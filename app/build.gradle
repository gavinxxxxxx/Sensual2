apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'AndResGuard'

def static autoVersionCode() {
    return Integer.parseInt(new Date().format("yyMMdd", TimeZone.getTimeZone("UTC")))
}

def static autoVersionName() {
    return new Date().format("yy.MM.dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "me.gavin.sensual"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode autoVersionCode()
        versionName autoVersionName()
        resConfigs "zh"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            versionNameSuffix ".debug"
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "API_DEBUG", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            versionNameSuffix ".release"
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "API_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }

    greendao {
        schemaVersion 1
        daoPackage 'me.gavin.sensual.db.dao'
        targetGenDir 'src/main/java'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

andResGuard {
    // @link{https://github.com/shwenzhang/AndResGuard/blob/master/README.zh-cn.md}
    mappingFile = null
    use7zip = true
    useSign = true
    keepRoot = false
    whiteList = [
            // nothing
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.10'
    }
    // v2签名会使得7zip压缩失效，如果你对apk大小有极致的要求，可以在signingConfigs中关闭v2签名
    // gradlew resguardRelease
    // adb shell monkey -p gavin.sensual -v 500
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    // implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation 'me.yokeyword:fragmentation:1.1.9'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation 'com.google.code.gson:gson:2.8.1'

    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    implementation 'cn.gavinliu.android.lib:ShapedImageView:0.8.4'

    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'

    implementation 'org.jsoup:jsoup:1.10.3'

    implementation 'org.greenrobot:greendao:3.2.2'

    implementation 'com.google.firebase:firebase-ads:11.6.2'
}

apply plugin: 'com.google.gms.google-services'
